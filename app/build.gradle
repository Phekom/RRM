/*
 * Updated by Shaun McDonald on 2021/05/18
 * Last modified on 2021/05/18, 11:09
 * Copyright (c) 2021.  XI Systems  - All rights reserved
 */

import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs"
    id "deploygate"
    id "eu.davidea.grabver"
    id "org.sonarqube"

}

versioning {
    major 1
    minor 6
    patch 0
}

static def getDate() {
    return new Date().format('yyyy-MM-dd HH-mm-ss')
}

static def safePropertyGetter(Properties properties, String key, String defaultValue) {
    System.out.println(key)
    if (properties.hasProperty(key)) {
        def result = '"' + properties[key] + '"'
        System.out.println(result)
        return result
    }

    return defaultValue
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

android {

    flavorDimensions "default"

    compileSdkVersion 32

    // Build Features
    buildFeatures {
        viewBinding true
        buildConfig true
    }

    def versionLabel =  "${versioning.name}.${versioning.build}"
    def versionBuild = 1
    def versionBuildDate = getDate()
//    def appId = "za.co.xisystems.itis_rrm"
    def appId = "za.co.xisystems.itismaintenance"

    // host properties
    def propertyFile = file(project.rootProject.file("host.properties").toString())
    System.out.println(propertyFile)
    Properties hostProps = new Properties()
    InputStream inputStream = new FileInputStream(propertyFile)
    hostProps.load(inputStream)
    inputStream.close()
    System.out.println(hostProps)

    signingConfigs {
        release {
            storeFile file(project.rootProject.file('release.RRMkey'))
            keyAlias 'rrmkey'
            storePassword 'thecowmakesmoo'
            keyPassword 'thecowmakesmoo'
        }
    }

    def qa_api = hostProps['QA_API'].toString()
    def dev_api = hostProps['DEV_API'].toString()
    def prod_api = hostProps['PROD_API'].toString()

// def registerAutoFillTest = safePropertyGetter(hostProps, 'REGISTER_AUTO_FILL_TEST', "false") as Boolean


    archivesBaseName = "rrm-app"

    versionBuildDate = getDate()

    defaultConfig {
        applicationId appId
        minSdkVersion 23
        targetSdkVersion 32
        versionName "$versionLabel"
        versionCode 1
        vectorDrawables.useSupportLibrary = false
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "VERSION_BUILD", "\"$versionBuild\"")
        buildConfigField("String", "VERSION_BUILD_DATE", '"' + versionBuildDate + '"')
        buildConfigField("String", "DNS_CHECK_IP4", "\"dns.google.com\"")
        testHandleProfiling true
        testFunctionalTest true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]

            }
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-defaults.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
            crunchPngs false
        }

        debug {
            testCoverageEnabled false
            // minifyEnabled false
            // useProguard false
            debuggable true
            crunchPngs false
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            signingConfig signingConfigs.debug
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    productFlavors {

        dev {
            splits.abi.enable = false
            splits.density.enable = false
            applicationId appId + ".dev"
            manifestPlaceholders = [app_name: "RRM-Dev", app_icon: "@mipmap/ic_launcher"]
            buildConfigField("String", "API_HOST", dev_api)
            versionName 'versioning.name'
            targetSdkVersion 32
            minSdkVersion 23
            testFunctionalTest true
            testHandleProfiling true
        }

        qa { // Sanral QA
            splits.abi.enable = false
            splits.density.enable = false
            applicationId appId + ".qa"
            manifestPlaceholders = [app_name: "RRM-Qa", app_icon: "@mipmap/ic_launcher"]
            buildConfigField("String", "API_HOST", qa_api)
            testFunctionalTest true
            testHandleProfiling true
        }

        prod {
            appId = "za.co.nra.itisrrm"
            applicationId appId
            manifestPlaceholders = [app_name: "RRM", app_icon: "@mipmap/ic_launcher"]
            buildConfigField("String", "API_HOST", prod_api)
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        compileOptions.encoding = "UTF-8"
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }

    buildToolsVersion '31.0.0'
    ndkVersion '23.0.7599858'

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'

    // LeakCanary
    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    debugImplementation 'com.squareup.leakcanary:leakcanary-object-watcher-android-androidx:2.7'
    implementation 'com.squareup.leakcanary:plumber-android:2.7'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    def activity_version = '1.4.0'

    // Kotlin
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.appcompat:appcompat:1.4.2"

    // Desugar
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'


    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // OkHttpProfiler
    debugImplementation 'com.itkacher.okhttpprofiler:okhttpprofiler:1.0.7'

    // Deploygate sdk
    implementation 'com.deploygate:sdk:4.3.0'

    // Argon2Kt - security pass 1
    implementation 'com.lambdapioneer.argon2kt:argon2kt:1.3.0'

    // JetSec - security pass 1
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    // Material
    implementation 'com.google.android.material:material:1.6.1'

    // Testing
    // Core library
    testImplementation 'androidx.test:core-ktx:1.4.0'

    // Robolectric allows device test to run on the JVM
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation 'io.mockk:mockk:1.12.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Mock api response unit tests
    testImplementation 'com.squareup.okhttp3:mockwebserver:5.0.0-alpha.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.4.0'

    // Layouts
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"

    // Navigation
    def nav_version = '2.4.2'

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"


    // secure exif from androidx
    implementation 'androidx.exifinterface:exifinterface:1.3.3'

    // sanselan
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'

    def lifecycle_version = '2.4.1'
    def arch_version = "2.1.0"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    // Annotation processor
    // kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"

    // optional - Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:$arch_version"


    // Recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Cardview
    implementation 'androidx.cardview:cardview:1.0.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.9.0'

    //Glide
    def glideVersion = '4.13.2'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    //Paging
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'

    //Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.3'

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Picasso & CropMe
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.takusemba:cropme:2.0.8'

    //Location
    implementation 'com.google.android.gms:play-services-location:20.0.0'

    def coroutine_version = '1.6.0'
    //Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"

    // testImplementation for pure JVM unit tests
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"

    // androidTestImplementation for Android instrumentation tests
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"

    // Better Date-time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'
    // implementation 'org.threeten:threetenbp:1.5.0'

    //Kodein Dependency Injection
    implementation 'org.kodein.di:kodein-di-framework-android-x:7.10.0'

    implementation 'com.github.dhaval2404:imagepicker:2.1'
    implementation 'com.github.yalantis:ucrop:2.2.6'

    // TouchImageView
    implementation 'com.github.tntkhang:full-screen-image-view-library:1.1.0'

    //Android  Room
    def room_version = '2.4.2' // align with Roomigrant

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    // implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    // Roomigrant
    implementation 'com.github.MatrixDev.Roomigrant:RoomigrantLib:0.3.4'
    kapt 'com.github.MatrixDev.Roomigrant:RoomigrantCompiler:0.3.4'


    // Groupie
    def groupie_version = "2.9.0"

    implementation "com.xwray:groupie:$groupie_version"
    implementation "com.xwray:groupie-viewbinding:$groupie_version"


    // preference
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // Jackson
    def jackson_version = '2.13.2'
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.0'
    implementation 'com.github.stfalcon-studio:StfalconImageViewer:v1.0.1'

    implementation 'org.apache.sanselan:sanselan:0.97-incubator'

    // Preview Image Collection
    implementation 'com.github.AgnaldoNP:PreviewImageCollection:2.3'

    // AndroidVeil
    implementation 'com.github.skydoves:androidveil:1.1.2'

    // ProgressView
    implementation 'com.github.skydoves:progressview:1.1.3'

    // androidx runtime permissions handler
    implementation 'com.permissionx.guolindev:permissionx:1.3.0'

    // Progress dialog drop in replacement
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'

    // Lottie Animations
    def lottieVersion = '4.2.2'
    implementation "com.airbnb.android:lottie:$lottieVersion"

    // Motion Toast
    def motionToastVersion = "1.4"
    implementation "com.github.Spikeysanju:MotionToast:$motionToastVersion"

    // Animatoo
    implementation 'com.github.mohammadatif:Animatoo:master'

    // NetworkResponseAdapter
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.0.1"


    implementation "org.springframework.android:spring-android-core:2.0.0.M3"

    // Jetpack Security
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

    // Password4J - security pass 2
    implementation 'com.password4j:password4j:1.5.4'

    // SQLCipher - Encrypts database at rest
    implementation 'net.zetetic:android-database-sqlcipher:4.5.0'
    implementation 'androidx.sqlite:sqlite-ktx:2.2.0'

    // TooLargeTool
    debugImplementation 'com.gu.android:toolargetool:0.3.0@aar'

    // Kotlin style Timber
    implementation 'com.github.ajalt:timberkt:1.5.1'

    // Security pass 3 - pin edit view
    implementation 'com.github.poovamraj:PinEditTextField:1.2.6'

    // NetworkResponseAdapter https://haroldadmin.github.io/NetworkResponseAdapter/
    implementation 'com.github.haroldadmin:NetworkResponseAdapter:4.0.1'

    //Mapbox
    implementation "com.mapbox.navigation:android:2.1.2"
    implementation "androidx.car.app:app:1.1.0"

    //No Internet available
    implementation 'am.appwise.components:NoInternetDialog:1.1.3'

}

task detektProjectBaseline(type: DetektCreateBaselineTask) {
    description = "Overrides current baseline."
    ignoreFailures.set(true)
    parallel.set(true)
    buildUponDefaultConfig.set(true)
    setSource(files(rootDir))
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    baseline.set(file("$rootDir/config/detekt/baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
}

ktlint {
    android.set(true)
    outputColorName.set("RED")
}